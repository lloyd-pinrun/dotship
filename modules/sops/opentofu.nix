{
  dotlib,
  lib,
  ...
}: {
  options.dotship.opentofu.workspaces = dotlib.options.attrs.submodule' ({config, ...}: {
    options.passwords = dotlib.options.attrs.attrs.anything "terraform random_password configs" {};

    options.sops = dotlib.options.attrs.submodule "secrets generated by terraform to store in sops" ({
      config,
      name,
      ...
    }: {
      options = {
        value = dotlib.options.str "terraform attribute reference with sops secret contents" {};
        path = dotlib.options.list.str "path in the default encrypted sops file to drop the secret" {default = [name];};

        env = dotlib.options.str "name of variable in a shell environment" {
          default = builtins.replaceStrings ["." "-"] ["__" "_"] (lib.toUpper name);
        };

        command = dotlib.options.str "command to save value in sops, running in the root directory" {
          default = let
            indexPath = lib.concatMapStrings (segment: "[\"${segment}\"]") config.path;
            envValue = "\\\"\$${config.env}\\\"";
          in "\${ local.SOPS_BIN } set \"\${ local.SOPS_DEFAULT }\" '${indexPath}' \"${envValue}\"";
        };
      };
    });

    config = lib.mkMerge [
      {
        modules = {
          flake,
          perSystem,
          ...
        }: let
          inherit (flake.config.dotship.sops) default directory;
        in {
          locals.SOPS_DEFAULT = "\${ var.GIT_DIR }/${default}";
          locals.SOPS_DIR = "\${ var.GIT_DIR }/${directory}";
          locals.SOPS_BIN = lib.getExe perSystem.config.dotship.sops.package;
        };
      }
      (lib.mkIf (config.passwords != {}) {
        plugins = ["hashicorp/random"];

        sops = lib.flip builtins.mapAttrs config.passwords (name: _: {
          path = ["passwords" name];
          value = "\${ random_password.${name}.result }";
        });

        modules.resource.random_password = config.passwords;
      })
      (lib.mkIf (config.sops != {}) {
        modules.resource.null_resource = lib.mkMerge [
          {
            sops = {
              triggers = builtins.mapAttrs (_: builtins.getAttr "value") config.sops;
              provisioner.local-exec = {
                environment =
                  lib.mapAttrs' (_: cfg: lib.nameValuePair cfg.env cfg.value) config.sops;

                command = lib.pipe config.sops [
                  (lib.mapAttrsToList (_: builtins.getAttr "command"))
                  (builtins.concatStringsSep "\n")
                ];
              };
            };
          }
          (lib.mkIf (config.kubernetes.cluster != null) {kubernetes.depends_on = ["null_resource.sops"];})
        ];
      })
    ];
  });
}
