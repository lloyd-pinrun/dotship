{lib, ...}: let
  inherit (builtins) getAttr replaceStrings;

  inherit
    (lib)
    concatMapStrings
    concatStringsSep
    flip
    getExe
    mapAttrs
    mapAttrs'
    mapAttrsToList
    nameValuePair
    mkIf
    mkMerge
    mkOption
    toUpper
    types
    ;
in {
  options.dotship.opentofu.workspaces = mkOption {
    type = types.lazyAttrsOf (types.submodule ({config, ...}: let
      inherit (config) kubernetes passwords sops;

      environment = mapAttrs' (_: cfg: nameValuePair cfg.env cfg.value) sops;
    in {
      options = {
        passwords = mkOption {
          type = types.attrsOf (types.attrsOf types.anything);
          default = {};
          description = "Terraform random_password configs";
        };

        sops = mkOption {
          type = types.lazyAttrsOf (types.submodule ({
            config,
            name,
            ...
          }: {
            options = {
              value = mkOption {
                type = types.str;
                description = "Terraform attribute reference with SOPS secret contents";
              };

              path = mkOption {
                type = types.listOf types.str;
                default = [name];
                description = "Path in the default encrypted SOPS file to drop the secret";
              };

              env = mkOption {
                type = types.str;
                default = replaceStrings ["." "-"] ["__" "_"] (toUpper name);
                description = "Name of environment variable in a shell";
              };

              command = mkOption {
                type = types.str;
                default = let
                  indexPath = concatMapStrings (segment: "[\"${segment}\"]") config.path;
                  envValue = "\\\"\$${config.env}\\\"";
                in "\${ local.SOPS_BIN } set \"\${ local.SOPS_DEFAULT }\" '${indexPath}'\"${envValue}\"";

                description = "Command to save value in SOPS, running in the project root directory";
              };
            };
          }));
          default = {};
          description = "Secrets generated by Terraform to store in SOPS";
        };
      };
      config = mkMerge [
        {
          modules = {
            flake,
            perSystem,
            ...
          }: let
            inherit (flake.config.dotship.sops) default directory;
          in {
            locals.SOPS_DEFAULT = "\${ var.GIT_DIR}/${default}";
            locals.SOPS_DIR = "\${ var.GIT_DIR}/${directory}";
            locals.SOPS_BIN = getExe perSystem.config.dotship.sops.package;
          };
        }
        (mkIf (passwords != {}) {
          plugins = ["hashicorp/random"];

          sops = flip mapAttrs passwords (name: _: {
            path = ["passwords" name];
            value = "\${ random_password.${name}.result }";
          });

          modules.resource.random_password = passwords;
        })
        (mkIf (sops != {}) {
          modules.resource.null_resource = mkMerge [
            {
              sops = {
                triggers = mapAttrs (_: getAttr "value") sops;
                provisioner.local-exec.environment = environment;
                provisioner.local-exec.command = concatStringsSep "\n" (mapAttrsToList (_: getAttr "command") sops);
              };
            }
            (mkIf (kubernetes.cluster != null) {
              kubernetes.depends_on = ["null_resources.sops"];
              kubernetes.provisioner.local-exec.environment = environment;
            })
          ];
        })
      ];
    }));
  };
}
